title Snail Boxes
author vexorian
homepage vexorian.dev

runtime_metadata_twiddling
run_rules_on_level_start
key_repeat_interval 0.25
sprite_size 16


========
TAGS
========

color = red blue yellow white
not_red = blue yellow
not_blue = red yellow
not_yellow = red blue

even_number = n0 n2 n4 n6 n8
odd_number  = n1 n3 n5 n7 n9
not_white   = blue yellow red
not_left    = up down right
========
MAPPINGS
========
even_number    => door_number
n0 n2 n4 n6 n8 -> n1 n3 n5 n7 n9

========
OBJECTS
========

Background 
#dda963

Block:white
#dda963 #25272a
................
.11111111111111.
.10000000000001.
.10000000000001.
.10000000000001.
.10000000000001.
.10000000010001.
.10000000000011.
.10000000000011.
.10100000000111.
.10000000100011.
.10000000000111.
.10000001101111.
.10001101111111.
.11111111111111.
................

Block:red
#cf6a4f  #25272a
copy:Block:white

Block:yellow
#e0b94a #25272a
copy:Block:white

Block:blue
#9b6970 #25272a
copy:Block:white


Player
#25272a #b2af5c
...0000000000...
..011111111110..
.01111111111110.
0111111111111110
0111111111111110
0111111111111110
0111111111111110
0111111111111110
0111111111111110
0111111111111110
0111111111111110
0111111111111110
0111111111111110
.01111111111110.
..011111111110..
...0000000000...

Target:even_number
#25272a
................
................
................
...0............
..000......00...
...000....00....
....000..00.....
.....00000......
......000.......
.....00000......
....00..000.....
...00....000....
..00......0.....
.00.............
................
................

ClosedDoor
#25272a transparent
1010101111010101
0000000000000000
1010101111010101
0000000000000000
1010101111010101
0000000000000000
1010101111010101
1010101111010101
1010101111010101
1010101111010101
0000000000000000
1010101111010101
0000000000000000
1010101111010101
0000000000000000
1010101111010101

NotOpeningDoor:white
#25272a #dda963
copy:ClosedDoor

NotOpeningDoor:red
#25272a #cf6a4f
copy:ClosedDoor

NotOpeningDoor:yellow
#25272a #e0b94a
copy:ClosedDoor

NotOpeningDoor:blue
#25272a #9b6970
copy:ClosedDoor


OpenDoor:odd_number
#25272a
................
.0.0.0....0.0.0.
................
.0.0.0....0.0.0.
................
.0.0.0....0.0.0.
................
................
................
................
.0.0.0....0.0.0.
................
.0.0.0....0.0.0.
................
.0.0.0....0.0.0.
................

Painted:yellow
#e0b94a
0000000000000.00
0000000000000000
0000000000000000
.000000000000000
000000000.000000
0000000000000000
0000000000000000
0000000000000000
.000000000000000
0000000000000000
00000.0000000000
0000000000000000
0000000000000000
0000000000000000
.00000000000000.
00000.00.0000000

Painted:red
#cf6a4f
copy:Painted:yellow

Painted:blue
#9b6970
copy:Painted:yellow

Painted:white
#dda963 
copy:Painted:yellow

Wall
#c9814b #9b6970 #25272a
0000000000000200
0000000000000200
0000000000100200
0000000000001200
0000000000011200
0000000010001200
0000000000101200
0000000111111200
2222222222222222
0000200000000000
0000200000000000
0001200000000000
1001200000000000
0011200000000000
1111200000000011
2222222222222222

Goal
#25272a #9b6970 #dbc1af 
................
....2...........
...2.2..........
....20000...2...
....022220.222..
....101101..2...
....022220......
.2..101101.0000.
2.2.022220022220
.2..011100101101
..00002220022220
.022220111101101
.101101220022220
.022220000101101
..0000....022220
...........0000.

Blocker
transparent

New
transparent

Border:left
#25272a
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............
0...............

Border:not_left
#25272a
copy:Border:left
rot:left:not_left

InvisibleCheckPoint
transparent


=======
LEGEND
=======

. = Background
P = Player       and Background
R = Block:red    and Background and Painted:red
Y = Block:yellow and Background and Painted:yellow
B = Block:blue   and Background and Painted:blue
# = Wall         and Background 
W = Block:white  and Background

Q = InvisibleCheckpoint

0 = Target:n0    and Background
2 = Target:n2    and Background
4 = Target:n4    and Background
6 = Target:n6    and Background
8 = Target:n8    and Background

1 = OpenDoor:n1  and Background and ClosedDoor
3 = OpenDoor:n3  and Background and ClosedDoor
5 = OpenDoor:n5  and Background and ClosedDoor
7 = OpenDoor:n7  and Background and ClosedDoor
9 = OpenDoor:n9  and Background and ClosedDoor

$ = Goal         and Background

SOLID = Player or Block:color or Wall or ClosedDoor or Blocker



=======
SOUNDS
=======

SFX1 31041700

NotOpeningDoor:color create afx:turn=0.01,0.01 79173707

Solid move left  afx:slide=0,0.1
Solid move right afx:slide=0,-0.1
Solid move up    afx:slide=0.1,0
Solid move down  afx:slide=-0.1,0

Block:color move 46513707


Solid cantmove left  afx:slide 78589302
Solid cantmove right afx:slide 78589302
Solid cantmove up    afx:slide 78589302
Solid cantmove down  afx:slide 78589302

Painted:color action afx:zoom

ClosedDoor create  79214904
ClosedDoor destroy 7734102

Painted:not_white destroy 72150108
Painted:not_white create  29387308

================
COLLISIONLAYERS
================

InvisibleCheckPoint
New
Background
Painted:color
Target:even_number, OpenDoor:odd_number 
Goal
Solid
Border:left
Border:right
Border:down
Border:up
NotOpeningDoor:color

======
RULES
======

[ Wall | no Wall ] -> [ Wall Border:> | no Wall ]


[ NotOpeningDoor:color ] -> []
[ Target:even_number no Solid ] -> [ Target:even_number New ]

color [ Block:color ] -> [ Block:color Painted:color ]

rigid color [ >  Player | Block:color ] -> [ > Player | > Block:color ]
rigid [ > Block:color | Block:color ] -> [ > Block:color | > Block:color ]

[ > Block:red    | Painted:not_red    Solid ] -> [ < Block:red    | action Painted:not_red    stationary Solid ]
[ > Block:yellow | Painted:not_yellow Solid ] -> [ < Block:yellow | action Painted:not_yellow stationary Solid ]
[ > Block:blue   | Painted:not_blue   Solid ] -> [ < Block:blue   | action Painted:not_blue   stationary Solid ]

[ > Block:red    | Painted:not_red    no Solid ] -> [ > Block:red    | action Painted:not_red    Blocker ]
[ > Block:yellow | Painted:not_yellow no Solid ] -> [ > Block:yellow | action Painted:not_yellow Blocker ]
[ > Block:blue   | Painted:not_blue   no Solid ] -> [ > Block:blue   | action Painted:not_blue   Blocker ]


late even_number [ Target:even_number no Solid ][ OpenDoor:door_number ] -> [ Target:even_number ][ OpenDoor:door_number New ]
late [ ClosedDoor no New ] -> []
late [ OpenDoor:odd_number New no Solid ] -> [ OpenDoor:odd_number ClosedDoor ]
late [ Blocker ] -> []
late color [ Block:color ] -> [ Block:color Painted:color ]

late [ OpenDoor:odd_number no Painted:color ] -> [ OpenDoor:odd_number Painted:white ]
late color even_number [ Target:even_number New Solid ][ ClosedDoor OpenDoor:door_number Painted:color no NotOpeningDoor:color ] -> [ Target:even_number New Solid ][ ClosedDoor OpenDoor:door_number Painted:color NotOpeningDoor:color ] 

late [ New ] -> []
late [ Player InvisibleCheckPoint ] -> [ Player ] checkpoint

late [ Player Goal ] -> SFX1

==============
WINCONDITIONS
==============

All Player on Goal


=======
LEVELS
=======


######################
#########00#...#####4#
#.......#..#.....#...#
#.......#..11.2..3.y.#
#.BB..#.#..##....#..4#
#####.R.....wb#..#5###
#......0...#..##y#5###
#Q..#####..##.#.y....#
#7###.############.###
#......#...9...8....##
#.y....#.$.9.#.r.#...#
#####6##...#.#b#.b.#.#
#....R.#####....r..88#
#..P...###88..#..#####
######################

(
######################
#########00#...#####4#
#.......#..#.....#...#
#.......#..11.2..3.Y.#
#.BB..#.#..##....#..4#
#####.R.....WB#..#5###
#......0...#..##Y#5###
#...#####..##.#.Y....#
#7################.###
#.Y.6.P.#..9.........#
#...#...#$.9.#888R.B.#
######################

)













